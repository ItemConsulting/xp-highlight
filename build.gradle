plugins {
  id 'java'
  id 'maven-publish'
  id 'com.enonic.xp.app' version '2.0.0'
  id 'no.item.xp.codegen' version '1.1.8'
}

app {
  name = "${appName}"
  displayName = "${appDisplayName}"
  vendorName = "${vendorName}"
  vendorUrl = "${vendorUrl}"
  systemVersion = "${xpVersion}"
}

repositories {
  mavenLocal()
  jcenter()
  xp.enonicRepo()
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

def highlightVersion = '10.1.2'

dependencies {
  include "com.enonic.xp:lib-portal:${xpVersion}"
  webjar "org.webjars:highlightjs:${highlightVersion}"
  webjar 'org.webjars.npm:highlightjs-line-numbers.js:2.8.0'
}

task createHighlightConfigJson {
  dependsOn += unpackWebJars

  def cssFolder = new File("${buildDir}/webjars/META-INF/resources/webjars/highlightjs/${highlightVersion}/styles")
  def langFolder = new File("${buildDir}/webjars/META-INF/resources/webjars/highlightjs/${highlightVersion}/languages")
  def destFile = new File("${rootDir}/src/main/resources/highlight-config.json")
  def builder = new groovy.json.JsonBuilder()

  builder {
    version highlightVersion
    cssFiles(
      cssFolder.listFiles()
        .findAll { it.name.contains(".min.css") }
        .collect { it.name }
    )
    languageFiles(
      langFolder.listFiles()
        .findAll {it.name.contains(".min.js")}
        .collect {it.name }
    )
  }

  destFile.write(builder.toPrettyString() + "\n")
  println("Updated \"${destFile.name}\"")
}

jar {
  dependsOn += createHighlightConfigJson
  dependsOn += generateJSDoc
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId group
      artifactId projectName
      version version
    }
  }
}
